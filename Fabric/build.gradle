plugins {
    id 'fabric-loom' version '0.10-SNAPSHOT'
    id 'maven-publish'
    id 'idea'
}

archivesBaseName = "${mod_name}-fabric-${minecraft_version}"

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.

    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }

    maven {
        name = 'Ladysnake Mods'
        url = 'https://ladysnake.jfrog.io/artifactory/mods'
    }
    maven { url "https://maven.shedaniel.me/" }

    maven {
        url "https://cfa2.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation project(":Common")

    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:4.1.1"
    include "dev.onyxstudios.cardinal-components-api:cardinal-components-base:4.1.1"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:4.1.1"
    include "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:4.1.1"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-world:4.1.1"
    include "dev.onyxstudios.cardinal-components-api:cardinal-components-world:4.1.1"



    modApi("me.shedaniel.cloth:cloth-config-fabric:6.0.42") {
        exclude(group: "net.fabricmc.fabric-api")
    }

//		modImplementation ("curse.maven:immortuos-411857:3542021")
    modCompileOnly ("curse.maven:immortuos-411857:3542021")

    modImplementation ("curse.maven:hordelib-571315:3761073")

    modImplementation "dev.emi:trinkets:${project.trinkets_version}"
}

loom {
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}


processResources {
    from project(":Common").sourceSets.main.resources
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }

    filesMatching('*.mixins.json') {
        expand "refmap_target": "${archivesBaseName}-"
    }
}

tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

jar {
    from("LICENSE") {
        rename { "${it}_${mod_name}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
